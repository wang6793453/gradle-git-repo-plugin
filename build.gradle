apply plugin: 'groovy'
apply plugin: 'maven'

group = 'com.layer'
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

apply from: "$rootDir/version.gradle"

repositories {
    mavenCentral()
    jcenter()
}

javadoc {
    options.charSet = 'UTF-8'
    options.encoding = 'UTF-8'
}


dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'org.ajoberstar:grgit:2.1.1'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file:${projectDir}/releases")
        }
    }
}

[install.repositories.mavenInstaller, uploadArchives.repositories.mavenDeployer]*.pom*.whenConfigured { pom ->
    pom.project {
        name 'Gradle Git Repo Plugin'
        packaging 'jar'
        description 'Use a git repo as a maven-style repo'
        url 'https://github.com/layerhq/gradle-git-repo-plugin'
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }
    }
}